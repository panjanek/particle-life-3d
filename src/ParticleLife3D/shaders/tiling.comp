#version 430

#pragma optimize(on)

layout(local_size_x = {LocalSizeX}) in;

struct ShaderConfig {
    int particleCount;
    float dt;
    float sigma2;
    float clampVel;
    float clampAcc;
    float fieldSize;
    float cellSize;
    float maxDist;
    int speciesCount;
    float damping;
    int trackedIdx;
    float maxForce;
    float amp;
    int cellCount;
    ivec4 disabled[4];
};

struct Particle
{
   vec4 position;
   vec4 velocity;
   int species;
   int flags;
   int  cellIndex;
   int  _pad1;
};

#define MAX_SPECIES_COUNT     10
#define KEYPOINTS_COUNT       6
#define FORCE_COUNT           (MAX_SPECIES_COUNT * MAX_SPECIES_COUNT * KEYPOINTS_COUNT)

layout(std140, binding = 0) uniform ConfigBuffer {
    ShaderConfig config;
};

layout(std430, binding = 1) buffer InputBuffer
{
    Particle particles[];
};

layout(std430, binding = 10) buffer SortedIndices {
    uint particleIndices[];
};

void torus_pos(inout vec4 pos)
{
    if (pos.x > config.fieldSize)
        pos.x -= config.fieldSize;
    else if (pos.x < 0)
        pos.x += config.fieldSize;

    if (pos.y > config.fieldSize)
        pos.y -= config.fieldSize;
    else if (pos.y < 0)
        pos.y += config.fieldSize;

    if (pos.z > config.fieldSize)
        pos.z -= config.fieldSize;
    else if (pos.z < 0)
        pos.z += config.fieldSize;
}

void torus_dist(inout vec4 d)
{
    if (abs(d.x) > config.fieldSize / 2)
        d.x -= config.fieldSize*sign(d.x);

    if (abs(d.y) > config.fieldSize / 2)
        d.y -= config.fieldSize*sign(d.y);

    if (abs(d.z) > config.fieldSize / 2)
        d.z -= config.fieldSize*sign(d.z);
}

int wrapCell(int c)
{
    return (c % config.cellCount + config.cellCount) % config.cellCount;
}


int assign_cell(vec4 pos)
{
    ivec4 cell = ivec4(pos / config.cellSize);
    cell.x = wrapCell(cell.x);
    cell.y = wrapCell(cell.y);
    cell.z = wrapCell(cell.z);
    return cell.x +
           cell.y * config.cellCount +
           cell.z * config.cellCount * config.cellCount;
}

void main()
{
    uint idx = gl_GlobalInvocationID.x;
    if (idx >=0 && idx < config.particleCount) 
    {
        int cellIndex = assign_cell(particles[idx].position);
        particles[idx].cellIndex = cellIndex;
        particleIndices[idx] = cellIndex;
    }
}